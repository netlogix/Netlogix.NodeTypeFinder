include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Fusion.Form/Private/Fusion/Root.fusion

Netlogix.NodeTypeFinder.NodeTypeFinderController.index = Netlogix.NodeTypeFinder:BackendModule

prototype(Netlogix.NodeTypeFinder:BackendModule) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <Neos.Fusion.Form:Form form.data.searchTerm={searchTerm} form.target.action="index">
            <div class="neos-row">
                <Neos.Fusion.Form:Neos.BackendModule.FieldContainer attributes.class="neos-span4" field.name="searchTerm" >
                    <Neos.Fusion.Form:Select attributes.class="neos-span4">
                        <Neos.Fusion:Loop items={nodeTypes} itemName="nodeType" @children="itemRenderer">
                            <Neos.Fusion.Form:Select.Option option.value={nodeType.name}>
                                {nodeType.label ? nodeType.label + ' (' + nodeType.name + ')' : nodeType.name}
                            </Neos.Fusion.Form:Select.Option>
                        </Neos.Fusion:Loop>
                    </Neos.Fusion.Form:Select>
                </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>
                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">
                    Search
                </Neos.Fusion.Form:Button>
            </div>
        </Neos.Fusion.Form:Form>
        <fieldset>
            <legend>Occurrences of NodeType</legend>
        </fieldset>
        <table class="neos-table" @if.hasOccurences={occurrences}>
            <Neos.Fusion:Loop items={occurrences} itemName="occurrence" @children="itemRenderer">
                <Netlogix.NodeTypeFinder:BackendModule.Occurrence/>
            </Neos.Fusion:Loop>
        </table>
        <div class="neos-row" @if.noOccurences={!occurrences && searchTerm}>
            <div class="neos-span12">
                No occurrences of NodeType {searchTerm}.
            </div>
        </div>
        <div class="neos-row" @if.noSearchTerm={!searchTerm}>
            <div class="neos-span12">
                Search for a NodeType to see a list of its occurrences.
            </div>
        </div>
    `
}

prototype(Netlogix.NodeTypeFinder:BackendModule.Occurrence) < prototype(Neos.Fusion:Component) {
    occurrence = ${occurrence}
    hiddenProps = Neos.Fusion:DataStructure {
        style = Neos.Fusion:Join {
            0 = 'color: #777'
            @glue = ';'
        }

        @if.hidden = ${occurrence.visible == false}
    }

    renderer = afx`
        <tr>
            <td>
                <a href={props.occurrence.url} {...props.hiddenProps}>
                    {props.occurrence.label}
                </a>
            </td>
            <td {...props.hiddenProps}>{props.occurrence.url}</td>
        </tr>
    `
}
